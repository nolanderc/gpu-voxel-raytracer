#version 450

layout(local_size_x = 16, local_size_y = 16) in;

layout(rgba32f, binding = 0) uniform writeonly image2D output_colors;
layout(rgba32f, binding = 1) uniform readonly image2D colors;

#define SIGMA_DISTANCE 1
#define SIGMA_RANGE 1
#define RADIUS 1

void main() {
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);

    ivec2 size = imageSize(output_colors);

    float normalization = 0;
    vec3 sum = vec3(0);

    float sigma_distance = 2 * pow(SIGMA_DISTANCE, 2);
    float sigma_range = 2 * pow(SIGMA_RANGE, 2);

    vec4 center = imageLoad(colors, coord);

    for (int dy = -RADIUS; dy <= RADIUS; dy++) {
        for (int dx = -RADIUS; dx <= RADIUS; dx++) {
            int nx = coord.x + dx;
            int ny = coord.y + dy;

            if (0 <= nx && nx < size.x && 0 <= ny && ny < size.y) {
                vec4 window = imageLoad(colors, ivec2(nx, ny));

                vec4 color_delta = center - window;
                float factor_range = dot(color_delta, color_delta) / sigma_range;
                float factor_distance = (dx*dx + dy*dy) / sigma_distance;
                float factor = exp(-factor_range - factor_distance);

                normalization += factor;
                sum += window.xyz * factor;
            }
        }
    }

    imageStore(output_colors, coord, vec4(RADIUS == 0 ? center.xyz : sum / normalization, 1));
}
